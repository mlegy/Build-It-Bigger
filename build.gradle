// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:2.2.3'

        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

allprojects {
    repositories {
        jcenter()
    }
}

task runBackendServer(dependsOn: ":GCELibrary:appengineRun") {
    group = "Custom Testing Tasks"
    description = "Start the backend server"
    project(":GCELibrary").afterEvaluate { backend ->
        backend.extensions.appengine.daemon = true
    }
    doLast {
        println "started the server!"
    }
}

task runTest {
    group = "Custom Testing Tasks"
    description = "Start the tests"
    dependsOn ':app:connectedAndroidTest'
    mustRunAfter runBackendServer
}

task shutdownBackendServer {
    group = "Custom Testing Tasks"
    description = "Shutdown the backend server"
    dependsOn ':GCELibrary:appengineStop'
    mustRunAfter runTest
    doLast {
        println "server shutdown!"
    }
}

task automate(dependsOn: [runBackendServer, runTest, shutdownBackendServer]) {
    group = "Automation"
    description = "Automate testing process"
    runTest.mustRunAfter runBackendServer
    shutdownBackendServer.mustRunAfter runTest
}